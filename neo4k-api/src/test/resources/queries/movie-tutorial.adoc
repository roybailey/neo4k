== Movie Tutorial Cypher ==

This is an example asciidoc record of cypher queries to illustrate
more external cypher maintenance with documentation as an alternative
to embedding into code itself.

==== `Find` ====

Example queries for finding individual nodes.

Click on any query example
Run the query from the editor
Notice the syntax pattern
Try looking for other movies or actors

```
// Find the actor named "Tom Hanks"
MATCH (tom {name: "Tom Hanks"}) RETURN tom

// Find the movie with title "Cloud Atlas"
MATCH (cloudAtlas {title: "Cloud Atlas"}) RETURN cloudAtlas

// Find 10 people
MATCH (people:Person) RETURN people.name LIMIT 10

// Find movies released in the 1990s
MATCH (nineties:Movie) WHERE nineties.released >= 1990 AND nineties.released < 2000 RETURN nineties.title
```


==== `Query` ====

Finding patterns within the graph.

Actors are people who acted in movies
Directors are people who directed a movie
What other relationships exist?

```
// List all Tom Hanks movies
MATCH (tom:Person {name: "Tom Hanks"})-[:ACTED_IN]->(tomHanksMovies) RETURN tom,tomHanksMovies

// Who directed "Cloud Atlas"?
MATCH (cloudAtlas {title: "Cloud Atlas"})<-[:DIRECTED]-(directors) RETURN directors.name

// Tom Hanks' co-actors
MATCH (tom:Person {name:"Tom Hanks"})-[:ACTED_IN]->(m)<-[:ACTED_IN]-(coActors) RETURN coActors.name

// How people are related to "Cloud Atlas"
MATCH (people:Person)-[relatedTo]-(:Movie {title: "Cloud Atlas"}) RETURN people.name, Type(relatedTo), relatedTo
```

==== `Solve` ====

You've heard of the classic "Six Degrees of Kevin Bacon"? That is simply a shortest path query called the "Bacon Path".

Variable length patterns
Built-in shortestPath() algorithm

```
// Movies and actors up to 4 "hops" away from Kevin Bacon
MATCH (bacon:Person {name:"Kevin Bacon"})-[*1..4]-(hollywood)
RETURN DISTINCT hollywood

// Bacon path, the shortest path of any relationships to Meg Ryan
MATCH p=shortestPath(
  (bacon:Person {name:"Kevin Bacon"})-[*]-(meg:Person {name:"Meg Ryan"})
)
RETURN p
```

